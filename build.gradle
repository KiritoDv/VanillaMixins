buildscript {
	repositories {
		mavenCentral()
		maven { url = "https://files.minecraftforge.net/maven" }
		maven { url = "https://repo.spongepowered.org/maven" }
		maven { url = "https://plugins.gradle.org/m2" }
	}

	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
		classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
		classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
	}
}

apply plugin: "net.minecraftforge.gradle.tweaker-server"
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"

version = "1.0.0"
group = "nessiesson.vanillamixins" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "vanillamixins"
def tweakerClass = "${group}.launch.VanillaMixinsTweaker"

minecraft {
	version = "1.12.2"
	mappings = "stable_39"
	runDir = "run"
	tweakClass = tweakerClass
	serverJvmArgs.addAll("-Dmixin.debug=true")

	makeObfSourceJar = false

	replaceIn "launch/VanillaMixins.java"
	replace "@MCVERSION@", version
	replace "@TWEAKER@", tweakerClass
}

reobf {
	jar {
		mappingType = "NOTCH"
	}
	shadowJar {
		mappingType = "NOTCH"
	}
}

mixin {
	defaultObfuscationEnv notch
}

sourceSets.main.ext.refMap = "mixins.vanillamixins.refmap.json"

repositories {
	maven { url = "https://repo.spongepowered.org/maven" }
}

dependencies {
	compile "org.spongepowered:mixin:0.8.1-SNAPSHOT"
	compile "org.ow2.asm:asm-debug-all:5.2"
	compile "net.sf.jopt-simple:jopt-simple:5.0.3"
	compile "com.eclipsesource.minimal-json:minimal-json:0.9.5"
	compile "org.apache.logging.log4j:log4j-api:2.8.1"
	compile "org.apache.logging.log4j:log4j-core:2.8.1"
	compile "com.google.guava:guava:21.0"
	compile "org.apache.commons:commons-lang3:3.5"
	compile "commons-io:commons-io:2.5"
	compile "com.google.code.gson:gson:2.8.0"
	provided "org.ow2.asm:asm-all:5.2"
}

shadowJar {
	classifier = ""
}
tasks.build.dependsOn(shadowJar)

jar.manifest.attributes(
		"Main-Class": "${group}.launch.VanillaMixins",
		"Class-Path": "minecraft_server.${minecraft.version}.jar libraries/net/minecraft/launchwrapper/1.12/launchwrapper-1.12.jar"
)
